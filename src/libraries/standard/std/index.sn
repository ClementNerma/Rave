/**
 * @file Standard Library definition file
 * @author Cl√©ment Nerma
 * @license Apache-2.0
 */

// NOTE: This file declares all entities natively available in SilverNight programs.
//       It is not ran at the beginning of each program but simply acts as a reference for developers and IDEs.

// NOTE: The body of many functions has been written in this file ; it only acts as a reference and may not
//        be implemented exactly the same way during build. Still, the functions' behaviour must remain the same.

// NOTE: Some declarations use a forbidden syntax like declaring as a plain member an instance of the current class.
//       This is allowed as this file only acts as a reference ; native types are self-dependent and so couldn't be
//        described another way.

// NOTE: This file is allowed to declare context-dependant constants without an initialization value.

// NOTE: This file is allowed to use native types or context-dependant constants before their declaration,
//        as some are self-dependants.

// NOTE: Unless otherwise stated, open classes are not inheritable from the outside of this file
//       The mention "NOTE: This class is inheritable" revokes this statement and allows the class to be inherited
//        by end-developer classes
//       Such classes also show their protected fields, as they can be used by their children

// Only module of the STD package
#[module];

// Frontend libraries are declared as extern
#[extern];

// ==================== Basic types ==================== //

/**
 * Generic type
 */
interface Any {}

/**
 * Static reference operator
 * @template DATA The data to get the class from
 * @returns The class 'DATA' is an instance of
 */
proxy! static<DATA: Any>: #raw<Class> {
  getter: flex () : #raw<Class> {
    return classof!(DATA);
  }
};

// Reference to the current instance's class
type _this = static<this>;

// Reference to the current class
type _self = static<self>;

// Reference to the current class' mother
type _super = static<super>;

// ==================== Typecastable types ====================

/**
 * Types convertible to a boolean
 */
interface BooleanConvertible {
  // Set a typecasting path for booleans
  func %to<bool> = string || f64 || f32 || u64 || i64 || u32 || i32 || u16 || i16 || u8 || i8;
}

/**
 * Types convertible to an integer
 */
interface IntegerConvertible {
  // Set a typecasting path for signed integers
  func %to<i64> = f64 || u64 || f32 || i32 || u32 || i16 || u16 || i8 || u8 || bool;

  // Set a typecasting path for unsigned integers
  func %to<u64> = f64 || i64 || f32 || u32 || i32 || u16 || i16 || u8 || i8 || bool;
}

/**
 * Types convertible to a floating-point number
 */
interface FloatConvertible {
  // Set a typecasting path for floating-point numbers
  func %to<f64> = f32 || u64 || i64 || u32 || i32 || u16 || i16 || u8 || i8 || bool;
}

/**
 * Types convertible to a number
 */
interface Numerizable {
  // Set a typecasting path for numbers
  func %to<number> = f64 || u64 || i64 || f32 || u32 || i32 || u16 || i16 || u8 || i8 || bool;
}

/**
 * Types which can be converted to a string
 */
interface Stringifyable {
  // Set a typecasting path for strings
  func %to<string> = f64 || u64 || i64 || f32 || u32 || i32 || u16 || i16 || u8 || i8 || bool;
}

// ==================== Clonable types ==================== //

/**
 * Clonable types
 */
interface Clonable {
  /**
   * Clone the current instance
   * @returns A clone of the current instance
   */
  func %clone () : _self;
}

// ==================== Serializable types ==================== //

/**
 * Serializable types
 */
interface Serializable {
  /**
   * Serialize the current instance
   * @returns The current instance as a string
   */
  func %serialize () : string;

  /**
   * Unserialize a string to an instance of the current type
   * @param serialized An instance of the current type, serialized
   * @returns An instance of this type
   */
  static func %unserialize (serialized: string) throws UnserializationError : _self;
}

// ==================== Comparable types ==================== //

/**
 * Comparable types
 */
interface Comparable {
  /**
   * Check if this instance is equal to another
   * @param another Another instance of this type
   * @returns `true` if this instance is equal to the provided one
   */
  func %equal (another: _self) : bool;

  /**
   * Check if this instance is greater than another
   * @param another Another instance of this type
   * @returns `true` if this instance is greater than the provided one
   */
  func %greater (another: _self) : bool;

  /**
   * Check if this instance is smaller than another
   * @param another Another instance of this type
   * @returns `true` if this instance is smaller than the provided one
   */
  func %smaller (another: _self) : bool;
}

// ==================== Randomizable types ==================== //

/**
 * Types able to generate a random value
 */
interface Randomizable {
  /**
   * Generate a random value of this type
   * @returns A random value of this type
   */
  static func %random () : _self;
}

/**
 * Types able to generate a random value between two bounds
 */
interface RandomizableWithBounds {
  /**
   * This type's minimum value
   */
  static val MINIMUM: _self;

  /**
   * This type's maximum value
   */
  static val MAXIMUM: _self;

  /**
   * Generate a random value of this type between two bounds
   * @param low The lower bound
   * @param up The upper bound
   * @returns A random value of this type
   */
  static func %random (low: _self, up: _self) : _self;
}

// ==================== Callable types ==================== //

/**
 * Callable types
 */
interface Callable {
  /**
   * Call this instance as a function
   */
  func %call(#__magic_arguments) : #__magic_return_type;
}

/**
 * Callable classes
 */
interface StaticallyCallable {
  /**
   * Call this class as a function
   */
  static func %call(#__magic_arguments) : #__magic_return_type;
}

// ==================== Primitive types ==================== //

/**
 * Primitive type
 */
virtual class Primitive implements BooleanConvertible, IntegerConvertible, FloatConvertible, Numerizable, Stringifyable, Clonable, Serializable, Comparable, StaticallyCallable {
  /**
   * NIL @class value
   */
  abstract public static val NIL: _self;

  /**
   * Make a NIL @class value
   */
  abstract public func %construct ();

  /**
   * Make a @class from another primitive value
   * @template T A primitive type
   * @param prim Another primitive value
   */
  abstract public func %construct<T ~ Primitive> (prim: T);

  /**
   * Convert this @class to a boolean
   * @returns This @class as a boolean
   */
  abstract public func %to<bool> () : bool;

  /**
   * Convert this @class to a 64-bit signed integer
   * @returns This @class as a 64-bit signed integer
   */
  abstract public func %to<i64> () : i64;

  /**
   * Convert this @class to a 64-bit unsigned integer
   * @returns This @class as a 64-bit unsigned integer
   */
  abstract public func %to<u64> () : u64;

  /**
   * Convert this @class to a 64-bit floating point number
   * @returns This @class as a 64-bit floating point number
   */
  abstract public func %to<f64> () : f64;

  /**
   * Convert this @class to a string
   * @returns This @class as a string
   */
  abstract public func %to<string> () : string;

  /**
   * Clone this @class
   * @returns A clone of this @class
   */
  abstract public func %clone () : _self;

  /**
   * Convert this @class to a string
   * @returns This @class as a string
   */
  abstract public func %serialize () : string;

  /**
   * Unserialize a string to a a @class
   * @returns The unserialized string as a a @class
   */
  abstract public static func %unserialize (serialized: string) throws UnserializationError : _self;

  /**
   * Check equality with another @class value
   * @param another Another @string value
   * @returns `true` if the two values are equal
   */
  abstract public func %equal (another: _self) : bool;

  /**
   * Check if this @class is greater than another
   * @returns `true` if this @class is greater than the other one
   */
  abstract public func %greater (another: _self) : _self;

  /**
   * Check if this @class is smaller than another
   * @returns `true` if this @class is smaller than the other one
   */
  abstract public func %smaller (another: _self) : _self;

  /**
   * Make a @string from another primitive value
   * @template T A primitive type
   * @param prim Another primitive value
   * @returns A @string made from the provided value
   */
  abstract public static func %call<T ~ Primitive> (prim: T) : _self;
}

// ==================== Object types ==================== //

/**
 * Object type
 */
type Object = not Primitive;

// ==================== Voids ==================== //

/**
 * Void type (primitive)
 */
class void inherits Primitive implements Randomizable, RandomizableWithBounds {
  public static val NIL = null;

  public func %construct () {};
  
  public func %construct<T ~ Primitive> (prim: T) => #__magic_set_primitive(this, prim);
  
  public func %to<bool> () : bool => false;
  
  public func %to<i64> () : i64 => 0l;
  
  public func %to<u64> () : u64 => 0ul;
  
  public func %to<f64> () : f64 => 0d;
  
  public func %to<string> () : string => '';
  
  public func %serialize () : string => '';
  
  public static func %unserialize (serialized: string) throws UnserializationError : _self => {
    if serialized != '' {
      throw new UnserializationError("Serialized voids can only be '' (empty string)");
    } else {
      return null;
    }
  };
  
  public func %equal (another: void) : bool => true;
  
  public func %greater (another: void) : void => false;
  
  public func %smaller (another: void) : void => false;
  
  public static func %call<T ~ Primitive> (prim: T) : void => null;

  /**
   * Generate a random boolean
   * @returns A random boolean
   */
  public static func %random () : void => null;

  /**
   * The lowest void value
   */
  public static val MINIMUM = null;

  /**
   * The highest void value
   */
  public static val MAXIMUM = null;

  /**
   * Generate a random void value between two bounds
   * @param low The lower bound
   * @param up The upper bound
   * @returns A random void between the provided bounds
   */
  public static func %random (low: void, up: void) : void => null;
}

// ==================== Booleans ==================== //

/**
 * Boolean type (primitive)
 * @classname(boolean)
 */
class bool inherits Primitive implements Randomizable, RandomizableWithBounds {
  public static val NIL = false;

  public func %construct () {};
  
  public func %construct<T ~ Primitive> (prim: T) => #__magic_set_primitive(this, prim);
  
  public func %to<bool> () : bool => this;
  
  public func %to<i64> () : i64 => this ? 1l : 0l;
  
  public func %to<u64> () : u64 => this ? 1ul : 0ul;
  
  public func %to<f64> () : f64 => this ? 1d : 0d;
  
  public func %to<string> () : string => this ? 'true' : 'false';
  
  public func %serialize () : string => this ? 'true' : 'false';
  
  public static func %unserialize (serialized: string) throws UnserializationError : bool {
    if serialized == 'true' {
      return true;
    } elsif serialized == 'false' {
      return false;
    } else {
      throw new UnserializationError("Serialized boolean can be either 'true' or 'false'");
    }
  }
  
  public func %equal (another: bool) : bool => this == another;
  
  public func %greater (another: bool) : bool => (this == true && another == false);
  
  public func %smaller (another: bool) : bool => (this == false && another == true);
  
  public static func %call<T ~ Primitive> (prim: T) : bool => prim != T.NIL;

  /**
   * Generate a random boolean
   * @returns A random boolean
   */
  public static func %random () : bool => #__magic_random_primitive(bool);

  /**
   * The lowest boolean value
   */
  public static val MINIMUM = false;

  /**
   * The highest boolean value
   */
  public static val MAXIMUM = true;

  /**
   * Generate a random boolean value between two bounds
   * @param low The lower bound
   * @param up The upper bound
   * @returns A random boolean between the provided bounds
   */
  public static func %random (low: bool, up: bool) : bool {
    if (low == true && up == false) ||
       (low == false && up == true) {
      return #__magic_random_primitive(bool);
    } else {
      // low == up
      return low;
    }
  }
}

// ==================== Numbers ==================== //

/**
 * Virtual number type (primitive)
 */
virtual class number inherits Primitive implements Randomizable, RandomizableWithBounds {
  public static val NIL = 0; // i32

  final public func %construct () {};
  
  final public func %construct<T ~ Primitive> (prim: T) => #__magic_set_primitive(self, prim);
  
  final public func %to<bool> () : bool => self != <self> 0;
  
  final public func %to<i64> () : i64 => #__magic_cast(i64, self);
  
  final public func %to<u64> () : u64 => #__magic_cast(u64, self);
  
  final public func %to<f64> () : f64  => #__magic_cast(f64, self);
  
  final public func %to<string> () : string => #__magic_cast(string, self);
  
  final public func %serialize () : string => #__magic_cast(string, self);
  
  final public static func %unserialize (serialized: string) throws UnserializationError : _self {
    #__magic_unserialize(_self, serialized);
  }
  
  final public func %equal (another: _self) : bool => self == another;
  
  final public func %greater (another: _self) : _self => self > another;
  
  final public func %smaller (another: _self) : _self => self < another;
  
  final public static func %call<T ~ Primitive> (prim: T) : _self => #__magic_cast(_self, prim);

  final public static func %random () : _self => #__magic_random_primitive(_self);

  public static val MINIMUM: _self;

  public static val MAXIMUM: _self;

  final public static func %random (low: _self, up: _self) : _self => #__magic_random_primitive(_self);

  /**
   * Is this number type signed?
   */
  abstract public static val SIGNED: bool;
  
  /**
   * Does this number type handle floating-point numbers?
   */
  abstract public static val HANDLE_FLOATS: bool;

  /**
   * On how many bits this number type fits?
   */
  abstract public static val SIZE: usize;

  /**
   * Generate a random @class
   * @returns A random @class
   */
  abstract public static func %random () : _self;

  /**
   * The lowest @class value
   */
  abstract public static val MINIMUM: _self;

  /**
   * The highest @class value
   */
  abstract public static val MAXIMUM: _self;

  /**
   * Generate a random @class value between two bounds
   * @param low The lower bound
   * @param up The upper bound
   * @returns A random @class between the provided bounds
   */
  abstract public static func %random (low: _self, up: _self) : _self;
}

/**
 * Virtual integer type (primitive)
 * @classname(virtual integer)
 */
open class vint inherits number {
  #superabstract;
  #supercstr;

  final public static val HANDLE_FLOATS = false;
}

/**
 * Virtual signed integer type (primitive)
 * @classname(virtual signed integer)
 */
open class vsint inherits vint {
  #superabstract;
  #supercstr;

  final public static val SIGNED = true;
}

/**
 * 64-bit signed integer type (primitive)
 */
open class i64 inherits vsint {
  #supercstr;

  public static val NIL = 0l;

  public static val SIZE = 64p;

  public static val MINIMUM = -9_223_372_036_854_775_808l;

  public static val MAXIMUM = 9_223_372_036_854_775_807l;
}

/**
 * 32-bit signed integer type (primitive)
 */
open class i32 inherits i64 {
  #supercstr;

  public static val NIL = 0;

  public static val SIZE = 32p;

  public static val MINIMUM = -2_147_483_648;

  public static val MAXIMUM = 2_147_483_647;
}

/**
 * 16-bit signed integer type (primitive)
 */
open class i16 inherits i32 {
  #supercstr;

  public static val NIL = 0s;

  public static val SIZE = 16p;

  public static val MINIMUM = -32_768s;

  public static val MAXIMUM = 32_767s;
}

/**
 * 8-bit signed integer type (primitive)
 */
class i8 inherits i16 {
  #supercstr;

  public static val NIL = 0b;

  public static val SIZE = 8p;

  public static val MINIMUM = -128b;

  public static val MAXIMUM = 127b;
}

/**
 * Virtual unsigned integer type (primitive)
 * @classname(virtual unsigned integer)
 */
open class vuint inherits vint {
  #superabstract;
  #supercstr;

  final public static val SIGNED = false;
}

/**
 * 64-bit unsigned integer type (primitive)
 */
open class u64 inherits vuint {
  #supercstr;

  public static val NIL = 0ul;

  public static val SIZE = 64p;

  public static val MINIMUM = 0ul;

  public static val MAXIMUM = 18_446_744_073_709_551_615ul;
}

/**
 * 32-bit unsigned integer type (primitive)
 */
open class u32 inherits u64 {
  #supercstr;

  public static val NIL = 0u;

  public static val SIZE = 32p;

  public static val MINIMUM = 0u;

  public static val MAXIMUM = 4_294_967_295u;
}

/**
 * 16-bit unsigned integer type (primitive)
 */
open class u16 inherits u32 {
  #supercstr;

  public static val NIL = 0us;

  public static val SIZE = 16p;

  public static val MINIMUM = 0us;

  public static val MAXIMUM = 65_535us;
}

/**
 * 8-bit unsigned integer type (primitive)
 */
class u8 inherits u16 {
  #supercstr;

  public static val NIL = 0ub;

  public static val SIZE = 8p;

  public static val MINIMUM = 0ub;

  public static val MAXIMUM = 255ub;
}

/**
 * Architecture-sized unsigned integer type (primitive)
 * Size is equal to the processor's number of bits
 */
class usize inherits uint {
  #supercstr;

  public static val NIL = 0p;

  public static val SIZE = PROC_ARCH_BITS;

  public static val MINIMUM = 0p;

  public static val MAXIMUM = 2p ** PROC_ARCH_BITS - 1p;
}

/**
 * Virtual signed floating-point number type (primitive)
 * @classname(virtual signed float)
 */
open class vfloat inherits number {
  #superabstract;
  #supercstr;

  final public static val SIGNED = true;

  final public static val HANDLE_FLOATS = true;
}

/**
 * 64-bit signed floating-point number type (primitive)
 * @classname
 */
open class f64 inherits vfloat {
  #supercstr;

  public static val SIZE = 64p;
}

/**
 * 32-bit signed floating-point number type (primitive)
 * @classname
 */
class f32 inherits f64 {
  #supercstr;

  public static val SIZE = 32p;
}

// ==================== Strings ==================== //

/**
 * String type (primitive)
 */
class string inherits Primitive {
  public static val NIL = '';

  public func %construct () {};
  
  public func %construct<T ~ Primitive> (prim: T) => #__magic_set_primitive(this, prim);
  
  public func %to<bool> () : bool => this.length > 0;
  
  public func %to<i64> () : i64 => #__magic_parse_string(this, i64);
  
  public func %to<u64> () : u64 => #__magic_parse_string(this, u64);
  
  public func %to<f64> () : f64 => #__magic_parse_string(this, f64);
  
  public func %to<string> () : string => this;
  
  /**
   * @example 'Hello \\ "guys"'.%serialize() == "Hello \\\\ \\"guys\\""
   */
  // DEVNOTE: * Add backslashes before each backslash
  //          * Add backslashes before each double quote
  //          * Replace all line breaks by their string equivalent
  //          * Wraps the string between double quotes
  public func %serialize () : string;

  public static func %unserialize (serialized: string) throws UnserializationError : string;

  // DEVNOTE: * If the strings are different, the strings are not equal
  //          * Compare each character one by one: if any is different, the strings are not equal
  //          * Else, they are equal
  public func %equal (another: bool) : bool;
  
  // DEVNOTE: * Compare each character one by one until the end of the shortest string:
  //            -> if this string's character has a greater ASCII code than the other, this string is greater
  //            -> if the other string's character has a greater ASCII code than the other, the other is greater
  //          * Else, if this string is longer than or equal to the other one, this string is greater
  //          * Else, the other string is greater than this one
  public func %greater (another: bool) : bool {
    val smallest_size = min!(@length, another.length);

    for i in 0..smallest_size {
      val this_code = @charCodeAt(i);
      val other_code = another.charCodeAt(i);

      return true if this_code > other_code;
      return false if other_code > this_code;
    }

    return (@length > another.length);
  }
  
  // DEVNOTE: * If this string is equal to the other one, it is not smaller
  //          * If this string is greater than the other one, it is not smaller
  //          * Else, this string is smaller than the other one
  public func %smaller (another: bool) : bool;
  
  public static func %call<T ~ Primitive> (prim: T) : string => <string> prim;

  /**
   * The first character
   */
  public val firstChar: string;

  /**
   * The last character
   */
  public val lastChar: string;

  /**
   * The number of characters in the string
   */
  public val length: u32;

  /**
   * Get the character a given position
   * @param pos The character's position
   * @returns The character
   */
  public func charAt (pos: u32) : string;

  /**
   * Get the ASCII code of a character at a given position
   * @param pos The character's position
   * @returns The character's ASCII code
   */
  public func charCodeAt (pos: u32) : uint;

  /**
   * Get the code point of a character at a given position
   * @param pos The character's position
   * @returns The character's code point
   */
  public func codePointAt (pos: u32) : uint;

  /**
   * Count the times a text appears in this string
   * @param lookfor The text to look for
   * @returns Number of times the given text appears in this string
   */
  public func count (lookfor: u32) : uint;

  /**
   * Cut the string
   * @param start The start position
   * @param end The end position
   * @returns The cut string, between the start and end position (both included)
   */
  public func cut (start: u32, end: u32) : string;

  /**
   * Check if the string ends with a text
   * @param lookfor The text to look for
   * @returns `true` if the string ends with the given text
   */
  public func endsWith (lookfor: string) : bool;

  /**
   * Prefix some characters with a backslash
   * @param chars A list of characters to escape
   * @returns The escaped string
   * @example "'Hello'".escape("'") == "\\'Hello\\'"
   * @samedef
   */
  public func escape (chars: Vector<string>) : string;

  /**
   * Get the first word of the string
   * @param sep The word seperator (default: white space)
   * @returns The string's first word
   */
  public func firstWord (sep: string = ' ') : string;

  /**
   * Get the index of a string inside this one
   * @param lookfor The string to look for
   * @returns The position of 'lookfor' in the string (null if not found)
   */
  public func indexOf (lookfor: string) : u32?;

  /**
   * Get the last index of a string
   * @param lookfor The string to look for
   * @returns The last position of 'lookfor' in the string (null if not found)
   */
  public func lastIndexOf (lookfor: string) : u32?;

  /**
   * Get the last word of the string
   * @param sep The word separator (default: white space)
   */
  public func lastWord (sep: string = ' ') : string;

  /**
   * Check if the string contains a text
   * @param lookfor The text to look for
   * @returns `true` if the text was found
   */
  public func includes (lookfor: string) : bool;

  /**
   * Check if the string is in another string
   * @param lookin The string that could contain this one
   * @returns `true` if this string is contained into the given one
   */
  public func isIn (lookin: string) : bool;

  /**
   * Check if the string is in a vector of string
   * @param lookin The vector that could contain this string
   * @returns `true` if this string is contained into the given vector
   */
  public func isIn (lookin: Vector<string>) : bool;

  /**
   * Parse the string as a number (recommended in front of numerical typecasting)
   * @template T The output format (must be a class inheriting from `number` or `number` itself)
   * @param base The input number's base (default: 10 for decimal)
   * @returns The parsed number
   * @example "2".parse() == "2" && "2.8".parse(float) == "2.8"
   */
  public func parse<T ~ number = int> (base: T = 10) throws StringParsingError : T;

  /**
   * Repeat the string a given amount of times
   * @param times The amount of times the string must be repeated
   * @returns The string, repeated
   */
  public func repeat (times: u32) : string;

  /**
   * Check if the string starts by a text
   * @param lookfor The text to look for
   * @returns `true` if the string starts by the given text
   */
  public func startsWith (lookfor: string) : bool;

  /**
   * Split the string into an array
   * @param model The separator
   * @returns An array containing each part of the array as an individual string
   */
  public func split (model: string) : string[];

  /**
   * Cut a string (fixed length)
   * @param start The start position
   * @param length The length
   * @returns The string cut from the start position with the given length
   */
  public func substr (start: u32, length: u32) : string;

  /**
   * Convert the string to base64
   * @returns This string as a base64 string
   */
  public func toBase64 () : string;

  /**
   * Convert the string to a list
   * @returns The string as a list of single characters
   */
  public func toList () : List<string>;

  /**
   * Convert the string to an array
   * @returns The string as an array of single characters
   */
  public func toArray () : string[];

  /**
   * Get the lowercase version of the string
   * @returns This string, lowercased
   */
  public func toLowerCase () : string;

  /**
   * Get the uppercase version of the string
   * @returns This string, uppercased
   */
  public func toUpperCase () : string;

  /**
   * Trim spaces at the beginning and the end of the string
   * @returns This string, trimmed
   */
  public func trim () : string;

  /**
   * Trim spaces at the beginning of the string
   * @returns This string, trimmed from the left
   */
  public func trimLeft () : string;

  /**
   * Trim spaces at the end of the string
   * @returns This string, trimmed for the right
   */
  public func trimRight () : string;

  /**
   * Get a word of the string
   * @param pos The word's index (starting from 0)
   * @param sep The word separator (default: white space)
   * @returns The word (in case of error, returns an empty string)
   */
  public func word (pos: u32, sep: string = ' ') : string;

  /**
   * Get all string's words (same as splitting with spaces)
   * @param sep Word separator (default: white space)
   * @returns The words
   */
  public func words (sep: string = ' ') : string[];

  /**
   * Decode a base64 string
   * @param base64 The base64 string to decode
   * @returns The given base64 string, decoded
   */
  public static func fromBase64 (base64: string) : string;

  /**
   * Make a string from a char code
   * @param code The char code to use
   * @returns The string made from the given char code
   */
  public static func fromCharCode (code: int) : string;

  /**
   * Make a string from a code point
   * @param code The code point to use
   * @returns The string made from the given code point
   */
  public static func fromCodePoint (code: int) : string;
}

// ==================== Tuples ==================== //

/**
 * Tuple type
 */
class Tuple {}

// ==================== Dictionaries ==================== //

/**
 * Dictionary type
 * This class is inherited by every dictionary class
 * NOTE: This class is inheritable
 * @template K Type of keys
 * @template V Type of values
 */
virtual dict Dictionary<K, V> {
  /**
   * Get the value associated to an existing key
   * @param key The key to get the value of
   * @returns The value associated to the key
   */
  abstract public func %get (key: K) : V;

  /**
   * Associate a value to a key
   * @param key The key to associate a value to
   * @param value The value to associate
   */
  abstract public func %set (key: K, value: V);

  /**
   * Delete an existing key (with its value)
   * @param key The key to delete
   */
  abstract public func %unset (key: K);
  
  /**
   * Check if a key exists in this dictionary
   * @param key The key to check the existence of
   * @returns `true` if the key exists
   */
  abstract public func %has (key: K) : bool;

  /**
   * Check if a value exists in this dictionary
   * @param value The value to check the existence of
   * @returns `true` if the value exists
   */
  abstract public func %contains (key: V) : bool;

  /**
   * Get an iterator on the keys
   * @returns An iterator on the keys
   */
  abstract public func %keys () : Iterator<K>;

  /**
   * Get an iterator on the values
   * @returns An iterator on the values
   */
  abstract public func %values () : Iterator<V>;
}

// ==================== Maps ==================== //

/**
 * Map type (powered-up dictionary)
 * NOTE: This class is inheritable
 * @template K Type of keys
 * @template V Type of values
 */
open dict Map<K, V> implements Clonable {
  /**
   * The @class's keys
   */
  protected keys: List<K>;

  /**
   * The @class's values
   */
  protected values: List<V>;

  /**
   * Make an empty @class
   */
  public func %construct ();

  /**
   * Initialize a @class by getting all the keys and values from another dictionary
   * @template X Type of the other dictionary's keys
   * @template Y Type of the other dictionary's values
   * @param init The dictionary to use for initialization
   */
  public func %construct<X ~ K, Y ~ V> (init: Dictionary<X, Y>);

  public func %get (key: K) : V;

  public func %set (key: K, value: V);

  public func %unset (key: K);

  public func %has (key: K) : bool;

  public func %contains (key: V) : bool;

  public func %keys () : Iterator<K>;

  public func %values () : Iterator<V>;

  /**
   * Make a new @class with the same keys and values
   * Keys and values will not be cloned themselves
   * @returns A clone of this @class
   */
  public func %clone () : _self;

  /**
   * Segment for clonable keys and values
   */
  segment (K ~ Clonable && V ~ Clonable) {
    /**
     * Make a new @class with the same keys and values
     * Keys and values will be cloned
     * @returns A clone of this @class
     */
    public func %clone () : _self;
  }

  /**
   * Segment for serializable keys and values
   */
  segment (K ~ Serializable && V ~ Serializable) implements Serializable, Stringifyable {
    /**
     * Stringify this @class, including its keys and values
     * @returns This @class, stringified
     */
    // DEVNOTE: * Stringify each string and value
    //          * Make a JSON object linking the stringified keys to the stringified values
    //          * Return the JSON object, as a string
    public func %serialize () : string;

    /**
     * Unserialize a stringified @class
     * @param serialized The @class to unserialize
     * @returns The unserialized @class
     */
    public static func %unserialize (serialized: string) throws UnserializationError : _self;

    /**
     * Stringify this @class, including its keys and values
     * @returns This @class, stringified
     */
    // DEVNOTE: Equivalent to %serialize()
    public func %to<string> () : string;
  }

  /**
   * Remove all keys and values from this @class
   */
  public func clear ();

  /**
   * Associate the same value to every existing key
   * @param value The value to associate
   */
  public func fill (value: V);

  /**
   * Filter this @class
   * @param filter The callback to filter the @class with
   * > @param key A @class's key
   * > @param value The value associated to this key
   * > @returns `false` if this key/value pair must be ignored
   * @returns A clone of this @class without the ignored key/value pairs
   * @example ([ 2, 5, -1 ]).filter((i, v) => v > 0) == [ 2, 5 ]
   */
  public func filter (filter: func (key: K, value: T) : bool) : _self;

  /**
   * Filter this @class
   * @param filter The callback to filter the @class with
   * > @param value A @class's value (order is respected)
   * > @returns `false` if this value must be ignored
   * @returns A clone of this @class without the ignored key/value pairs
   * @example ([ 2, 5, -1 ]).filter(i => i > 0) == [ 2, 5 ]
   */
  public func filter (filter: func (value: T) : bool) : _self;

  /**
   * Insert all the keys and values from another dictionary
   * @template X Type of the other dictionary's keys
   * @template Y Type of the other dictionary's values
   * @param merge The dictionary to use for merging
   */
  public func merge<X ~ K, Y ~ V> (merge: Dictionary<X, Y>);

  /**
   * Segment for comparable keys and values
   */
  segment (T ~ Comparable) implements Comparable {
    /**
     * Check if this @class is equivalent to another dictionary
     * @param compareTo The @class to compare this one
     * @returns `true` if the dictionaries have the same number of keys and values and if they are equal
     */
    public func %equal (compareTo: Dictionary<K, V>) : bool;

    /**
     * Check if this @class is greater than another one (always `false`)
     * @param compareTo The @class to compare this one
     * @returns `false`
     */
    public func %greater (compareTo: Dictionary<K, V>) : bool => false;

    /**
     * Check if this @class is smaller than another one (always `false`)
     * @param compareTo The @class to compare this one
     * @returns `false`
     */
    public func %smaller (compareTo: Dictionary<K, V>) : bool => false;
  }

  /**
   * Generate a new @class from this one through a callback
   * The callback is called for each key/value pair
   *  its return value is transparently stored in a new @class
   * @template X Type of keys returned by the callback
   * @template Y Type of values returned by the callback
   * @param generator The generator callback
   * > @param key A @class's key
   * > @param value The value associated to the provided key
   * > @returns A key/pair value to add to the ghost @class
   * @returns The ghost @class generated using the callback
   * @example ({ # 'hello': 'world' }).@class((k, v) => (v, k)) == ({ # 'world': 'hello' })
   */
  public func @class<X = K, Y = V> (callback: func (key: K, value: V) : (X, Y)) : @class<X, Y>;

  /**
   * Generate a new @class from this one through a callback
   * Keys are kept ; only values can be generated
   * The callback is called for each key/value pair
   *  its return value is transparently stored in a new @class, linked to the current key
   * @template Y Type of values returned by the callback
   * @param generator The generator callback
   * > @param key A @class's key
   * > @param value The value associated to the provided key
   * > @returns A value to add to the ghost @class
   * @returns The ghost @class generated using the callback (same keys)
   * @example ({ # 'hello': 'world' }).@class((k, v) => 'mom') == ({ # 'hello': 'mom' })
   */
  public func @class<Y = V> (callback: func (key: K, value: V) : Y) : @class<K, Y>;

  /**
   * Make a @class from an array of keys and an array of values
   * Keys and values are mapped in the same order they are in the provided vector
   * @template X Type of keys
   * @template Y Type of values
   * @template SIZE Size of the two arrays
   * @param keys The list of keys
   * @param values The list of values
   * @returns The @class generated from the provided lists
   */
  public static func fromArrays<X, Y, SIZE: usize> (keys: X[SIZE], values: Y[SIZE]) : @class<X, Y>;
}

// ==================== Map-based types ==================== //

/**
 * Collection of values (map with string keys)
 * @template T Type of values
 * @classname(collection)
 */
type Collection<T> = Map<string, T>;

/**
 * Sortable map
 * @template K Type of keys
 * @template V Type of values
 * @classname(sortable map)
 */
type SortableMap<K ~ Sortable, V ~ Sortable> = Map<K, V>;

/**
 * Comparable map
 * @template K Type of keys
 * @template V Type of values
 * @classname(comparable map)
 */
type ComparableMap<K ~ Comparable, V ~ Comparable> = Map<K, V>;

/**
 * Map with unique values
 * NOTE: This class is inheritable
 * @template K Type of keys
 * @template V Type of values
 */
open class Set<K, V ~ Comparable> inherits Map<K, V> {
  /** @classname */

  #supercstr;

  /**
   * Associate a value to a key
   * NOTE: A 'DuplicateValueError' will be thrown if the provided value is already contained
   * NOTE: As all assignment methods like `.merge()` use this overload, they may throw this error too
   * @param key The key to associate a value to
   * @param value The value to associate
   */
  abstract public func %set (key: K, value: V);
}

// ==================== Context-dependant constants ==================== //

// NOTE: [WARNING] The values context-dependant constants can take are not fixed for now.
//                 They can considerably evolve before the stable version of the language.

/**
 * Processor architecture
 * @example 'x86' ; 'arm'
 */
val PROC_ARCH: string;

/**
 * Number of bits of the processor (32 or 64 bits only)
 * @example 32p ; 64p
 */
val PROC_ARCH_BITS: usize;

/**
 * Name of the operating system
 * @example 'windows' ; 'linux'
 */
val OS_NAME: string;

/**
 * Platform name
 * NOTE: Equivalent of `uname -a` on Linux
 * @example 'Linux someone-laptop 4.15.0-29-generic #31-Ubuntu SMP Tue Jul 17 15:39:52 UTC 2018 x86_64 x86_64 x86_64 GNU/Linux'
 */
val PLATFORM: string;
